@echo off

:: ---------------------------------------------------------------------------
:: TCC-RT Unit Test Worker
:: Anthony J. Borla (ajborla@bigpond.com)
:: ---------------------------------------------------------------------------

::
:: Perform tests
::
set STATUS=0
do testargs in @CON:
   gosub :RUNTEST %testargs
   if %RUNTEST EQ FAIL set STATUS=1
enddo

::
:: Ensure test status returned
::
gosub :EXIT %STATUS

::
:: Subroutines
::
:RUNTEST [name cmpop expcode expvalue cut*]
   :: Execute code-under-test, collect results:
   :: %RETVALUE
   :: %RETCODE
   gosub :TESTCUT %cut

   :: Determine test status, and act accordingly
   iff %@IF[%@QUOTE[%expvalue] %cmpop %@QUOTE[%RETVALUE],1,0] EQ 1 .AND. ^
       %@IF[%expcode EQ %RETCODE,1,0] EQ 1 then
      set RUNTEST=OK
   else
      set RUNTEST=FAIL
   endiff

   :: Print test outcome
   echo %RUNTEST - %name
return

:TESTCUT [cut*]
   :: Capture return value
   echos @set RETVALUE=>RETVALUE.btm
   call %cut >>RETVALUE.btm
   :: Suppress expansion of redirection and piping characters
   setdos /X-56
   call RETVALUE
   :: Restore expansions, and cleanup temporary file
   setdos /X0
   del/q RETVALUE.btm 2> NUL:

   :: Capture return code
   call %cut 1> NUL:
   set RETCODE=%errorlevel
return

:EXIT [code]
   unset RETCODE RETVALUE STATUS RUNTEST
   quit %code
return

